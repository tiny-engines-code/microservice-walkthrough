plugins {
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
}

group = 'com.chrislomeli'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()

}

ext {
    set('springCloudVersion', "2020.0.3")
    set('cukesVersion', "6.11.0")
    set('mockitoVersion', "3.11.2")
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    // sendgrid
    implementation group: 'com.sendgrid', name: 'sendgrid-java', version: '4.7.0'

    // testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation group: 'com.jayway.jsonpath', name: 'json-path', version: '2.6.0'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: "${mockitoVersion}"
    testImplementation group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: "${mockitoVersion}"
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.20.2'

    // wiremock
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2'

    // Cucumber
    testImplementation group: 'io.cucumber', name: 'cucumber-java', version:  "${cukesVersion}"
    testImplementation group: 'io.cucumber', name: 'cucumber-junit-platform-engine', version: "${cukesVersion}"
    testImplementation group: 'io.cucumber', name: 'cucumber-spring', version: "${cukesVersion}"
    testImplementation group: 'io.cucumber', name: 'cucumber-core', version: "${cukesVersion}"

    // OpenAPI
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.5.10'

}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/chrislomeli/**/SendgridRequest.class",
             ])
        }))
    }
}